apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

//Bintray depends on this global variable to set the library version!
version = '0.8.3'
group = 'br.com.simplepass'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'LocationProgressBar'
            description 'A progress bar to show location progress'
            url 'https://github.com/leandroBorgesFerreira/LocationProgressBar'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'br.com.simplepass'
            artifactId 'location-progress-bar'
            version '0.8.3'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/leandroBorgesFerreira/LocationProgressBar.git'
                url 'https://github.com/leandroBorgesFerreira/LocationProgressBar'

            }
            developers {
                developer {
                    name 'Leandro Borges Ferreira'
                }
            }
        }
    }
}

// Configure gradle-bintray-plugin (for publishing releases)
bintray {
    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = 'LocationProgressBar'
        userOrg = 'lehen01'
        licenses = ['Apache-2.0']
        labels = ['customview', 'animation']
        websiteUrl = 'https://github.com/leandroBorgesFerreira/LocationProgressBar'
        issueTrackerUrl = 'https://github.com/leandroBorgesFerreira/LocationProgressBar/issues'
        vcsUrl = 'https://github.com/leandroBorgesFerreira/LocationProgressBar.git'
        publicDownloadNumbers = true
    }

}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
}
